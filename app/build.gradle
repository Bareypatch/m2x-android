apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

def clientVersion = "5.0.0"
def mavenGroup = "com.att.m2x"

archivesBaseName = "android"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 6
        versionName clientVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.mcxiaoke.volley:library:1.0.9'
    compile 'com.google.code.gson:gson:2.3.1'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            pom.project {
                groupId mavenGroup
                version clientVersion
                name 'AT&T M2X Android Library'
                packaging 'jar'
                artifactId 'android'
                description 'An Android library for interacting with the AT&amp;T M2X API.'
                url 'http://m2x.att.com'

                scm {
                    connection 'scm:git:git@github.com:attm2x/m2x-android.git'
                    developerConnection 'scm:git:git@github.com:attm2x/m2x-android.git'
                    url 'git@github.com:attm2x/m2x-android.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'kristinpeterson'
                        name 'Kristin Peterson'
                        email 'kristin.peterson@citrusbyte.com'
                        organization 'Citrusbyte'
                        organizationUrl 'http://www.citrusbyte.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = mavenGroup
}
